configurations {
    bundleJar {
        transitive = false
    }
}

allprojects {

    group "me.sasanqua.utils"
    version "1.0.0"

    ext {
        spongeVersion = '8.0.0'
        forgeVersion = '1.16.5-36.2.20'
        forgeMappingChannel = 'official'
        forgeMappingVersion = '1.16.5'
    }

    repositories {
        mavenCentral()
        maven { url 'https://repo.spongepowered.org/maven' }
    }
}


subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    jar {
        from project(":Common").sourceSets.getByName("main").output
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }

}

dependencies {
    subprojects.each {
        bundleJar it
    }
}

tasks.register("buildAll") { task ->

    task.dependsOn configurations.named("bundleJar")

    task.doFirst {
        copy {
            from subprojects.collect { file("./${it.name}/build/libs/${it.name}.jar") }
            eachFile {
                it.setName("${it.name.replace(".jar", "")}Utils.jar")
            }
            into file("./build/libs/")
        }
    }

    task.doLast {
        delete {
            it.delete subprojects.collect { file("${it.name}/build/") }
        }
    }

}